# These globals store important information such as where we're downloading
# the rootfs to, and where we're unpacking it.  These constants are initialized
# by `__init__()` to allow for environment variable overrides from the user.
downloads_cache = ""
rootfs_cache = ""
shards_cache = ""
automatic_apple = false
use_squashfs = false

# This is where the `sandbox` binary lives
const sandbox_path = joinpath(dirname(@__FILE__), "..", "deps", "sandbox")

"""
    downloads_dir(postfix::String = "")

Builds a path relative to the `downloads_cache`.
"""
function downloads_dir(postfix::String = "")
    global downloads_cache
    return joinpath(downloads_cache, postfix)
end

"""
    rootfs_dir(postfix::String = "")

Builds a path relative to the `rootfs_cache`.
"""
function rootfs_dir(postfix::String = "")
    global rootfs_cache
    return joinpath(rootfs_cache, postfix)
end


"""
    shards_dir(postfix::String = "")

Builds a path relative to the `shards_cache`.
"""
function shards_dir(postfix::String = "")
    global shards_dir
    return joinpath(shards_cache, postfix)
end


"""
    get_shard_url(target::String = "base"; squashfs::Bool = use_squashfs)

Returns the URL from which a rootfs image (tarball/squashfs) can be downloaded
"""
function get_shard_url(target::String = "base"; squashfs::Bool = use_squashfs)
    # These constants are what should be updated for a new rootfs build:
    rootfs_urlroot = "https://julialangmirror.s3.amazonaws.com/binarybuilder"
    rootfs_version = "2017-11-29"

    shard_name = "rootfs-$(target)"
    ext = squashfs ? "squashfs" : "tar.gz"

    return "$(rootfs_urlroot)-rootfs-$(target)-$(rootfs_version).$(ext)"
end

"""
    get_shard_url(target::String = "base"; squashfs::Bool = use_squashfs)

Returns the sha256 hash for a rootfs image (tarball/squashfs).
"""
function get_shard_hash(triplet::String = "base"; squashfs::Bool = use_squashfs)
    # These constants are what should be updated for a new rootfs build.
    # You can get these dictionaries spat out by running `make print-hashes`
    # in `julia-docker/crossbuild`, after running `make shards`.
    squashfs_hashes = Dict(
        "aarch64-linux-gnu" => "4bc3ed1e8baf3ad242f113f207ece1b8f883c09abfd6fbd5cae1c5cb658c942e",
        "arm-linux-gnueabihf" => "77f56e8419596d0cc2a537b51888ccdcce31be67a40d4b05bc98ad43c3707d7d",
        "base" => "8fcef4d92d5554dc9293849bfda00e7c19987d8a7e06bb24c25c43c030021b0e",
        "i686-linux-gnu" => "d18869e3dbbbae99599551ee55e2d02062244f4de4f77f162e1b97eb157a9cad",
        "i686-w64-mingw32" => "36fc9bcd2ce0e2715a6f926ea5776d0c61f797d7766c35f2bb5076132dbdb9bb",
        "powerpc64le-linux-gnu" => "4af7872e6b984884b22e0f609d9549080b234ce7737546d53ab52a510ae62111",
        "x86_64-apple-darwin14" => "95c729308c1749ce6f07d5af81ef7e7553ebb940d44a034c374878e39766f0f1",
        "x86_64-linux-gnu" => "4ee9e93b19e9df9d351b06bbead581df25e11fae3b4fe464735e522a83e3124e",
        "x86_64-w64-mingw32" => "66e5d6647d9968ff5474ba672b231b046a49ee75adaeff73e3a3a2d84afa4868",
    )
    tarball_hashes = Dict(
        "aarch64-linux-gnu" => "6aef48d32aec550598ac646193384752880620345b6012888abed3acd34f4aea",
        "arm-linux-gnueabihf" => "7853b74137e416989130ccca211ebf3e4f54a50b8431ab131526c180a6238f91",
        "base" => "5775bea9fa099dabc353070e54a87af501f239d702c686e5da6b53b12e2210bb",
        "i686-linux-gnu" => "72b073102276fb13ebab243bcee01b4bd6d1119380a5c54b23ad3f0b9bb33ab8",
        "i686-w64-mingw32" => "66620705d2f9cc3a750dc84f07570418c78c22730948ca5616796678e9b07083",
        "powerpc64le-linux-gnu" => "02f5d0ddc9d9afec59fabc9687155166ec4ee9e55e59404d562ea83173344d8e",
        "x86_64-apple-darwin14" => "7a53fa8ff02651a54a8ed0404e8c5332ea54a448a00c320df537acec6a080e90",
        "x86_64-linux-gnu" => "0dbf9e1be7f7eb748ee41ba7159cd6001666cfb8ff83af4a5c7ca829b0537633",
        "x86_64-w64-mingw32" => "d597387aeafa36a77c5933b3d897fd1e03d5b22c3006f8a28bba6bab095f8347",
    )

    if squashfs
        return squashfs_hashes[triplet]
    else
        return tarball_hashes[triplet]
    end
end

# Note: produce these values by #including squashfs_fs.h from linux in a Cxx
# and running the indicated command
const offsetof_id_table_start = 0x30    # offsetof(struct suqashfs_super_block, id_table_start)
const offsetof_no_ids = 0x1a            # offsetof(struct suqashfs_super_block, no_ids)

# From squashfs_fs.h
const SQUASHFS_COMPRESSED_BIT = UInt16(1) << 15
const SQUASHFS_MAGIC = 0x73717368

"""
    rewrite_squashfs_uids(path, new_uid)
    
In order for the sandbox to work well, we need to have the uids of the squashfs
images match the uid of the current unpriviledged user. Unfortunately there is
no mount-time option to do this for us. However, fortunately, squashfs is simple
enough that if the id table is uncompressed, we can just manually patch the uids
to be what we need. This functions performs this operation, by rewriting all
uids/gids to new_uid.
"""
function rewrite_squashfs_uids(path, new_uid)
    open(path, "r+") do file
       # Check magic
       if read(file, UInt32) != SQUASHFS_MAGIC
           error("`$path` is not a squashfs file")
       end
       # Check that the image contains only one id (which we will rewrite)
       seek(file, offsetof_no_ids)
       if read(file, UInt16) != 1
           error("`$path` uses more than one uid/gid")
       end
       # Find the index table
       seek(file, offsetof_id_table_start)
       offset = read(file, UInt64)
       seek(file, offset)
       # Find the correct metdata block
       index = read(file, UInt64)
       seek(file, index)
       # Read the metadata block
       size = read(file, UInt16)
       # Make sure it's uncompressed (yes, I know that flag is terribly
       # named - it indicates that the data is uncompressed)
       if ((size & SQUASHFS_COMPRESSED_BIT) == 0)
           error("Metadata block is compressed")
       end
       p = position(file)
       uid = read(file, UInt32)
       # TODO: We should distribute these files with uid 0
       # if uid != 0
       #    error("Expected all uids to be 0 inside the image")
       # end
       seek(file, p)
       write(file, UInt32(new_uid))
    end
end

"""
    update_rootfs(triplets::Vector{AbstractString};
                  automatic::Bool = automatic_apple, verbose::Bool = true,
                  squashfs::Bool = use_squashfs)

Updates the stored rootfs containing all cross-compilers and other compilation
machinery for the given triplets.  If `automatic` is set, when downloading Apple
SDKs, you will automatically accept the Apple license agreement and download the
macOS SDK for usage in targeting macOS.  See the help for `download_osx_sdk()`
for more details on this.
"""
function update_rootfs(triplets::Vector{S}; automatic::Bool = automatic_apple,
                       verbose::Bool = true, squashfs::Bool = use_squashfs) where {S <: AbstractString}
    # Check to make sure we have the latest version of both the base and the
    # given shard downloaded properly, and extracted if it's not a squashfs.
    for shard_name in ["base", triplets...]
        url = get_shard_url(shard_name; squashfs=squashfs)
        hash = get_shard_hash(shard_name; squashfs=squashfs)

        # Actually make the destination directory
        if shard_name == "base"
            dest_dir = rootfs_dir()
        else
            dest_dir = shards_dir(shard_name)
        end

        if squashfs
            squashfs_path = downloads_dir("rootfs-$(shard_name).squashfs")
            
            
            file_existed = isfile(squashfs_path)
            
            # If squashfs, verify/redownload the squashfs image
            if !(download_verify(
                url,
                hash,
                squashfs_path;
                verbose = verbose,
                force = true
            ) && file_existed)
                # Unmount the mountpoint. It may point to a previous version
                # of the file. Also, we're about to mess with it
                if success(`mountpoint $(dest_dir)`)
                    run(`sudo umount $(dest_dir)`)
                end

                # Patch squashfs files for current uid
                rewrite_squashfs_uids(squashfs_path, ccall(:getuid, Cint, ()))
                
                # Touch SHA file to prevent re-verifcation from blowing the
                # fs away
                touch(string(squashfs_path,".sha256"))
            end
            
            # Then mount it, if it hasn't already been mounted:
            if !success(`mountpoint $(dest_dir)`)
                mkpath(dest_dir)
                run(`sudo mount $(squashfs_path) $(dest_dir) -o ro,loop`)
            end
        else
            # If tarball, verify/redownload/reunpack the tarball
            download_verify_unpack(
                url,
                hash,
                dest_dir;
                tarball_path = downloads_dir("rootfs-$(shard_name).tar.gz"),
                verbose = verbose,
                force = true,
            )
        end
    end

    # If we're targeting the macOS SDK here, make sure it's ready to go.
    if any(triplets .== "x86_64-apple-darwin14")
        download_osx_sdk(;automatic=automatic, verbose=verbose)
    end
end

# Helper for when you're only asking for a single triplet
update_rootfs(triplet::AbstractString; kwargs...) = update_rootfs([triplet]; kwargs...)

"""
    download_osx_sdk(;automatic::Bool = automatic_apple, verbose::Bool = false,
                      version::AbstractString = "10.10")

Apple restricts distribution and usage of the macOS SDK, a necessary component
to build software for macOS targets.  Please read the Apple and Xcode SDK
agreement for more information on the restrictions and legal terms you agree to
when using the SDK to build software for Apple operating systems:
https://images.apple.com/legal/sla/docs/xcode.pdf.

If `automatic` is set, this method will automatically agree to the Apple usage
terms and download the macOS SDK, enabling building for macOS.

To set this on an environment level, set the `BINARYBUILDER_AUTOMATIC_APPLE`
environment variable to `"true"`.
"""
function download_osx_sdk(;automatic::Bool = automatic_apple,
                           verbose::Bool = false,
                           version::AbstractString = "10.10")
    urlbase = "https://github.com/phracker/MacOSX-SDKs/releases/download/10.13"
    
    # Right now, we only support one version, but in the future, we may need
    # to support multiple macOS SDK versions
    sdk_metadata = Dict(
        "10.10" => (
            "$(urlbase)/MacOSX10.10.sdk.tar.xz",
            "4a08de46b8e96f6db7ad3202054e28d7b3d60a3d38cd56e61f08fb4863c488ce",
            shards_dir("MacOSX10.10.sdk"),
        ),
    )
    
    if !haskey(sdk_metadata, version)
        error("Unknown macOS version $(version); cannot download SDK!")
    end
    url, hash, dest = sdk_metadata[version]

    # If it already exists, return out
    if isdir(dest)
        if verbose
            info("macOS SDK $(dest) already exists")
        end
        return
    end

    # Ask if we should download it, unless we're automated
    if !automatic
        if !isinteractive()
            msg = strip("""
            This is not an interactive Julia session, so we will not prompt you
            to download and install the macOS SDK, see the docstring for the
            `download_osx_sdk()` method for more details.
            """)
            warn(msg)
            return
        end
        msg = strip("""
        macOS SDK not yet downloaded!

        Apple restricts distribution and usage of the macOS SDK, a necessary
        component to build software for macOS targets.  Please read the Apple
        and Xcode SDK agreement for more information on the restrictions and
        legal terms you agree to when using the SDK to build software for Apple
        operating systems: https://images.apple.com/legal/sla/docs/xcode.pdf.
        """)
        print_with_color(:bold, msg)
        println()
        while true
            print("Would you like to download and use the macOS SDK? [y/N]: ")
            answer = lowercase(strip(readline(STDIN)))
            if answer == "y" || answer == "yes"
                break
            elseif answer == "n" || answer == "no"
                return
            else
                println("Unrecognized answer. Answer `y` or `n`.")
            end
        end
    else
        if verbose
            info("Automatic macOS SDK install initiated")
        end
    end
    
    download_verify_unpack(
        url,
        hash,
        dest;
        tarball_path=downloads_dir(basename(url)),
        verbose=verbose,
        force=true
    )

    # These macOS tarballs have a nasty habit of nesting, let's fix that:
    dir_in_dir = joinpath(dest, basename(dest))
    if isdir(dir_in_dir)
        mv(dir_in_dir, "$(dirname(dir_in_dir))2")
        mv("$(dirname(dir_in_dir))2", dest; remove_destination=true)
    end
end


"""
    update_sandbox_binary(;verbose::Bool = true)

Builds/updates the `sandbox` binary that launches all commands within the
rootfs, storing the binary within the `deps` folder.
"""
function update_sandbox_binary(;verbose::Bool = true)
    global sandbox_path

    src_path = joinpath(dirname(@__FILE__), "..", "deps", "sandbox.c")
    if !isfile(sandbox_path) || stat(sandbox_path).mtime < stat(src_path).mtime
        if verbose
            info("Rebuilding sandbox binary...")
        end
        
        cd() do
            oc = OutputCollector(
                `gcc -o $(sandbox_path) $(src_path)`;
                verbose=verbose,
            )
            wait(oc)
        end
    end
end
